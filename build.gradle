buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

version = "${FCR_Ver_Major}.${FCR_Ver_Minor}.${FCR_Ver_Patch}"
group = "frogcraftrebirth"
archivesBaseName = "FrogCraft-Rebirth"

if (System.getenv('PUBLIC_BUILD') == null) {
    version += "+" + "git rev-parse --short HEAD".execute().text.trim() + "-SNAPSHOT"
}

repositories {
    maven {
        name = "jei"
        url = "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to"
    }
    maven {
        name = "jared"
        url = "https://maven.blamejared.com"
    }
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"

    mappings = "stable_39"
    
    replace "@VERSION@", project.version
}

jar {
    manifest {
        attributes 'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}"
        attributes 'Timestamp': System.currentTimeMillis()
    }
}

dependencies {
    compileOnly "net.industrial-craft:industrialcraft-2:2.8.221-ex112:api"
    compileOnly "mezz.jei:jei_1.12.2:4.16.1.302:api"

    deobfCompile("CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.594") {
        exclude group: 'org.ow2.asm'
        exclude group: 'com.google.code.gson'
    }

    implementation "mezz.jei:jei_1.12.2:4.16.1.302"
    implementation "net.industrial-craft:industrialcraft-2:2.8.221-ex112"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task apiJar(type: Jar, dependsOn: jar) {
    classifier = 'api'
    from (sourceSets.main.output) {
        include "frogcraftrebirth/api/**"
    }
}

artifacts {
    archives apiJar
}
