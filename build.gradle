buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

version = "${FCR_Ver_Major}.${FCR_Ver_Minor}.${FCR_Ver_Patch}"
group = "frogcraftrebirth"
archivesBaseName = "FrogCraft-Rebirth"

if (System.getenv('PUBLIC_BUILD') == null) {
    version += "+" + "git rev-parse --short HEAD".execute().text.trim()
}

repositories {
    maven {
        name = "jei"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to"
    }
    maven {
        name = "jared"
        url = "http://maven.blamejared.com"
    }
}

minecraft {
    version = "${Minecraft_Ver}-${MinecraftFogre_Ver}"
    runDir = "run"

    mappings = Mapping_Ver
    
    replace "@VERSION@", project.version
}

dependencies {
    provided "net.industrial-craft:industrialcraft-2:${IC2_Ver}-ex112:api"
    provided "mezz.jei:jei_${Minecraft_Ver}:${JEI_Ver}:api"

    deobfCompile "net.industrial-craft:industrialcraft-2:${IC2_Ver}-ex112"
    deobfCompile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-${CraftTweaker_Ver}"
    deobfCompile "CraftTweaker2:CraftTweaker2-API:${CraftTweaker_Ver}"
    deobfCompile "CraftTweaker2:ZenScript:${CraftTweaker_Ver}"

    runtime "mezz.jei:jei_${Minecraft_Ver}:${JEI_Ver}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task devJar(type: Jar, dependsOn: jar) {
    classifier = 'dev'
    from sourceSets.main.output
}

task apiJar(type: Jar, dependsOn: jar) {
    classifier = 'api'
    from (sourceSets.main.output) {
        include "frogcraftrebirth/api/**"
    }
}

artifacts {
    archives devJar
    archives apiJar
}
